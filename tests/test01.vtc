varnishtest "some tests"

server s1 {
	rxreq
	txresp -hdr "baz: qux" -body "9876543210"
	expect req.url == "/my/url"
	expect req.http.foo == "bar"
	expect req.body == "0123456789"
} -start

varnish v1 -arg "-p thread_pool_stack=160k" -vcl+backend {
	import reqwest from "${vmod}";

	sub vcl_recv {
		reqwest.init("secure", "GET", "https://example.com");
		reqwest.send("secure");

		reqwest.init("faulty", "GET", "....");

		reqwest.init("s1", "GET", "http://${s1_addr}:${s1_port}/my/url");
		reqwest.set_header("s1", "foo", "bar");
		reqwest.set_body("s1", "0123456789");
		return (synth(200));
	}

	sub vcl_synth {
		reqwest.init("regular", "GET", "http://example.com");
		set resp.http.regular_status = reqwest.status("regular");
		set resp.http.secure_status = reqwest.status("secure");
		set resp.http.faulty_status = reqwest.status("faulty");
		set resp.http.faulty_error = reqwest.error("faulty");
		set resp.http.s1_baz = reqwest.header("s1", "baz");
		set resp.http.s1_body = reqwest.body_as_string("s1");
	}
} -start

client c1 {
	txreq
	rxresp
	expect resp.http.secure_status == 200
	expect resp.http.regular_status == 200
	expect resp.http.faulty_status == 0
	expect resp.http.faulty_error == "builder error: relative URL without a base"
	expect resp.http.s1_baz == "qux"
	expect resp.http.s1_body == "9876543210"
} -run

server s1 -wait
